#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Recollection \"Tree Ocean of Hourai\""]
#Text["Code by whitegrimm [r] Recollection \"Tree Ocean of Hourai\""]
#Background["script/default_system/Default_Background_IceMountain.txt"]

let ZUNbullet = GetCurrentScriptDirectory() ~ "./AllStarShot/AllStarShot.dnh";
let objBoss;
let objScene = GetEnemyBossSceneObjectID();
let count = -120;

@Event{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE){
		SetScriptResult(3000);
	}
	case(EV_REQUEST_TIMER){
		SetScriptResult(60);
	}
	case(EV_REQUEST_SPELL_SCORE){
		SetScriptResult(1000000);
	}
}

@Initialize{
	LoadTexture(ZUNbullet);
	LoadEnemyShotData(ZUNbullet);
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	ObjMove_SetDestAtFrame(objBoss, GetCenterX(), GetCenterY() - 150, 60);
	ObjEnemyBossScene_StartSpell(objScene);
	
	TDrawLoop;
	TFinalize;
}

@MainLoop{
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
	
	/*if(count > 0 && count % 180 == 0){
		ObjMove_SetDestAtFrame(objBoss, rand(GetCenterX() + 90, GetCenterX() - 90), rand(GetCenterY() - 60, GetCenterY() - 120), 60);
	}
	*/
	if(count == 0){fireDark;}
	if(count % 210 == 0 && count >= 560){fireRing;}
	
	count++;
	yield;
}

task fireRing {
	if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0){return;} //Default kill to prevent (0, 0) spawning
	wait(5);
	let angleT = rand(0, 360);
	ascent(i in 0..7){
		loop(20){
			if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0){return;}//Default kill to prevent (0, 0) spawning
			CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 2.5, angleT, 702+i, 0);
			angleT += 360/20;
		}
		angleT += 360/rand(2, 8);
		wait(30);
	}
}

task fireDark {
	if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0){return;} //Default kill to prevent (0, 0) spawning
	wait(15);
	let angleT = 0;
	let angleU = 270;
	loop{
		loop(2){
			ascent(i in 0..7){
				if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0){return;}//Default kill to prevent (0, 0) spawning
				let obj = CreateShotA1(GetCenterX+28*cos(angleU), GetCenterY+35+28*sin(angleU), 2, angleT, 130+i*2, 0);
				fireLight(obj, 51+i*2);
				angleU += 360/7;
			}
			angleT += 360/2;
		}
		angleT += 3.5;
		wait(7);
		yield;
	}
}

task fireLight(obj, graph) {
	while(ObjMove_GetX(obj) > 0 && ObjMove_GetX(obj) < GetStgFrameWidth && ObjMove_GetY(obj) > 0 && ObjMove_GetY(obj) < GetStgFrameHeight){yield;}
	wait(5);
	if(ObjMove_GetX(obj) <= 0){
		ObjMove_SetX(obj, 0);
		ObjMove_SetAngle(obj, GetAngleToPlayer(obj));
	}else if(ObjMove_GetX(obj) >= GetStgFrameWidth){
		ObjMove_SetX(obj, GetStgFrameWidth);
		ObjMove_SetAngle(obj, GetAngleToPlayer(obj));
	}else if(ObjMove_GetY(obj) <= 0){
		ObjMove_SetY(obj, 0);
		ObjMove_SetAngle(obj, GetAngleToPlayer(obj));
	}else if(ObjMove_GetY(obj) >= GetStgFrameHeight){
		ObjMove_SetY(obj, GetStgFrameHeight);
		ObjMove_SetAngle(obj, GetAngleToPlayer(obj));
	}
	ObjShot_SetGraphic(obj, graph);
}

task TDrawLoop {
	let imgExRumia = GetCurrentScriptDirectory() ~ "ExRumia.png";
	ObjPrim_SetTexture(objBoss, imgExRumia);
	ObjSprite2D_SetSourceRect(objBoss, 64, 1, 127, 64);
	ObjSprite2D_SetDestCenter(objBoss);
}

task TFinalize {
	while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0){yield;}
	if(ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SHOOTDOWN_COUNT) + ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SPELL_COUNT) == 0){
		AddScore(ObjEnemyBossScene_GetInfo(objScene, INFO_SPELL_SCORE));
	}
	Obj_Delete(objBoss);
	DeleteShotAll(TYPE_ALL, TYPE_ITEM);
	SetAutoDeleteObject(true);
	CloseScript(GetOwnScriptID());
	return;
}

function GetCenterX(){
		return GetStgFrameWidth() / 2;
}
function GetCenterY(){
		return GetStgFrameHeight() / 2;
}

function wait(n){
	loop(n){yield;}
}